apply plugin: 'java'

ext {
	LibClassesDir = "$buildDir/libclasses"
	LocalArtifactDir = "D:/Lib/doclava"
}

group = 'org.knight.doclava'
archivesBaseName = 'doclava'
version = '1.0.6'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
	options.warnings = false
}

repositories {
	mavenCentral()
}

dependencies {
	compile files(System.env["JAVA_HOME"] + "/lib/tools.jar")
	compile 'com.google.guava:guava:18.0'
	compile 'com.google.jsilver:jsilver:1.0.0'

	compile 'org.slf4j:slf4j-api:1.7.21'
	compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'
	compile 'org.apache.logging.log4j:log4j-core:2.5'

	compile 'org.apache.commons:commons-io:1.3.2'

	compile 'com.github.jknack:handlebars:4.0.5'
}

jar {
	dependsOn 'unzipLibraries'
	from LibClassesDir
}

uploadArchives {
	repositories {
		flatDir {
			name 'LocalArtifactDir'
			dir LocalArtifactDir
		}
	}
}

task unzipLibraries {
	outputs.dir file(LibClassesDir)
	doLast {
		configurations.compile.each { lib ->
			if (lib.name != 'tools.jar') {
				copy {
					from zipTree(lib)
					into LibClassesDir
				}
			}
		}
	}
}
